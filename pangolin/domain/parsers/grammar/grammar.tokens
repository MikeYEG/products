program
    : application
    | language
    | script
    ;

script
    : SCRIPT COLON scriptValue+ DOT
    ;

scriptValue
    : SCRIPT_NAME COLON NAME_PATTERN DOT
    | SCRIPT_VERSION COLON VERSION_PATTERN DOT
    | SCRIPT_SCRIPT COLON relativePath DOT
    | SCRIPT_LANGUAGE COLON relativePath DOT
    ;

language
    : LANGUAGE COLON languageValue+ DOT
    ;

languageValue
    : LANG_ROOT COLON PATTERN_PATTERN DOT
    | LANG_TOKENS COLON relativePath DOT
    | LANG_CHANNELS COLON relativePath DOT
    | LANG_RULES COLON relativePath DOT
    | LANG_LOGIC COLON relativePath DOT
    | IN COLON VARIABLE_PATTERN DOT
    | OUT COLON VARIABLE_PATTERN DOT
    | EXTENDS COLON singleExtend+ DOT
    | LANG_PATTERN_MATCHES COLON patternMatch+ DOT
    ;

singleExtend
    : relativePath DOT
    ;

patternMatch
    : PATTERN_PATTERN COLON patternLabels DOT
    ;

patternLabels
    : patternLabelEnter patternLabelExit
    | patternLabelEnter? patternLabelExit
    | patternLabelEnter patternLabelExit?
    ;

patternLabelEnter
    : LABEL_PATTERN
    ;

patternLabelExit
    : PIPE LABEL_PATTERN
    ;

relativePath
    : QUOTATION folderSection+ QUOTATION
    ;

folderSection
    : folderName SLASH
    | FILE_PATTERN
    ;

folderName
    : DOT
    | DOT DOT
    | FILE_PATTERN
    ;

application
    : headSection labelSection? mainSection testSection?
    ;

testSection
    : TESTS COLON testDeclaration+ DOT
    ;

testDeclaration
    : TEST_NAME_PATTERN COLON testInstruction+ DOT
    ;

testInstruction
    : instruction
    | assert DOT
    | readFile DOT
    ;

assert
    : ASSERT
    | ASSERT_IF COLON VARIABLE_PATTERN
    ;

readFile
    : READ COLON VARIABLE_PATTERN COMMA relativePath
    ;

headSection
    : HEAD COLON headValue+ DOT
    ;

headValue
    : NAME COLON NAME_PATTERN DOT
    | VERSION COLON VERSION_PATTERN DOT
    | IMPORTS COLON importSingle+ DOT
    ;

importSingle
    : NAME_PATTERN COLON relativePath DOT
    ;

labelSection
    : LABELS COLON labelDeclaration+ DOT
    ;

labelDeclaration
    : LABEL_PATTERN COLON labelInstruction+ DOT
    ;

labelInstruction
    : instruction
    | RET DOT
    ;

mainSection
    : MAIN COLON instruction+ DOT
    ;

instruction
    : variable DOT
    | operation DOT
    | print DOT
    | stackFrame DOT
    | jump DOT
    | match DOT
    | exit DOT
    | call DOT
    | token DOT
    ;

token
    : CODE_MATCH COLON codeMatch
    | CODE COLON tokenSection
    ;

codeMatch
    : VARIABLE_PATTERN COMMA VARIABLE_PATTERN COMMA callPatterns
    ;

tokenSection
    : specificTokenCodeWithAmount
    | specificTokenCode
    | VARIABLE_PATTERN
    ;

specificTokenCodeWithAmount
    : VARIABLE_PATTERN COMMA VARIABLE_PATTERN COMMA callPattern
    ;

specificTokenCode
    : VARIABLE_PATTERN COMMA callPattern
    ;

callPatterns
    : callPattern pipeCallPattern*
    ;

pipeCallPattern
    : PIPE callPattern
    ;

callPattern
    : COMMERCIAL_A patternOrRule
    ;

patternOrRule
    : RULE_PATTERN
    | PATTERN_PATTERN
    ;

variable
    : declaration
    | assignment
    | concatenation
    | DELETE COLON VARIABLE_PATTERN
    ;

concatenation
    : CONCAT COLON standardOperation
    ;

declaration
    : DECLARE COLON VARIABLE_PATTERN PIPE type
    ;

assignment
    : ASSIGN COLON VARIABLE_PATTERN COMMA value
    ;

value
    : numericValue
    | boolValue
    | stringValue
    | NIL
    | VARIABLE_PATTERN
    ;

numericValue
    : MINUS? INT
    | MINUS? floatValue
    ;

boolValue
    : TRUE
    | FALSE
    ;

floatValue
    : INT DOT INT
    ;

stringValue
    : QUOTATION EVERYTHING_EXCEPT_QUOTATION QUOTATION
    ;

type
    : NIL
    | BOOL
    | INT_HEIGHT
    | INT_SIXTEEN
    | INT_THIRTY_TWO
    | INT_SIXTY_FOUR
    | FLOAT_THIRTY_TWO
    | FLOAT_SIXTY_FOUR
    | UINT_HEIGHT
    | UINT_SIXTEEN
    | UINT_THIRTY_TWO
    | UINT_SIXTY_FOUR
    | STRING
    ;

operation
    : arythmetic
    | relational
    | logical
    ;

arythmetic
    : ADD COLON standardOperation
    | SUB COLON standardOperation
    | MUL COLON standardOperation
    | DIV COLON remainingOperation
    ;

relational
    : LESS_THAN COLON standardOperation
    | EQUAL_INS COLON standardOperation
    | NOT_EQUAL_INS COLON standardOperation
    ;

logical
    : AND COLON standardOperation
    | OR COLON standardOperation
    ;

transformOperation
    : VARIABLE_PATTERN COMMA VARIABLE_PATTERN
    ;

standardOperation
    : VARIABLE_PATTERN COMMA VARIABLE_PATTERN COMMA VARIABLE_PATTERN
    ;

remainingOperation
    : VARIABLE_PATTERN COMMA VARIABLE_PATTERN COMMA VARIABLE_PATTERN COMMA VARIABLE_PATTERN
    ;

print
    : PRINT COLON value
    ;

match
    : MATCH COLON VARIABLE_PATTERN matchPattern?
    ;

matchPattern
    : COMMA DOLLAR_SIGN PATTERN_PATTERN
    ;

jump
    : JUMP COLON PERCENT LABEL_PATTERN
    | JUMP_IF COLON VARIABLE_PATTERN COMMA PERCENT LABEL_PATTERN
    ;

exit
    : EXIT
    | EXIT_IF COLON VARIABLE_PATTERN
    ;

call
    : CALL COLON NAME_PATTERN
    | CALL_IF COLON VARIABLE_PATTERN COMMA NAME_PATTERN
    ;

stackFrame
    : PUSH
    | POP
    | skip
    | index
    ;

skip
    : SKIP COLON intPointer
    ;

index
    : INDEX COLON VARIABLE_PATTERN
    ;

intPointer
    : INT
    | VARIABLE_PATTERN
    ;
